!! Noun inflection
! ---------------
!! The Lushootseed language nouns inflect in time person aspect.
! s%<ɬalil%>tu%>b%>s%>əxʷ
! VNoun+come.ashore+V+Caus+Mid+Px3+Now
! np+come·ashore+[caus]+[md]+3s+now
! %>tu ~ %>t
! %>əb ~ %>b


LEXICON N_with-PL_RED1_qelb
!: DIM_CV1toCV2C ; What should this be?
: SG-SUF ;
: PL_RED1_qelb ;

LEXICON N_with_PL_RED2
: DIM_CV1toCV2C ;
: SG-SUF ;
: PL_RED2 ;

LEXICON N_with_Distr_12323_RED5 !Der/Red5_12323
!%^Red12323
!: DIM_CV1toCV2C ;
: SG-SUF ;
: PL_RED5 ;

LEXICON N-FEM_with-PL_RED2
+Fem: DIM_CV1toCV2C ;
+Fem: SG-SUF ;
+Fem: PL_RED2 ;

LEXICON N-MSC_with-PL_RED2
+Msc: DIM_CV1toCV2C ;
+Msc: SG-SUF ;
+Msc: PL_RED2 ;

! Derivation
LEXICON DIM_CV1toCV2C
@D.PossPx@+Der+Der/Dimin+N:@D.PossPx@%^Red1CV1toCV2C SG-SUF ; !+Der/Red1_1i13
!@D.PossPx@+Der+Der/Dimin+N+Sg+Nom:@D.PossPx@%^Red1CV1toCV2C # ;
@R.PossPx@+Der+Der/Dimin+N:@R.PossPx@%^Red1CV1toCV2C SG-SUF ; !+Der/Red1_1i13
@D.PossPx@+Der+Der/Dimin+N+Distr+Nom:@D.PossPx@%^Red1CV1toCV2CPL PRE-POSS_01 ;! +Der/Red1_1i13_Distr_1212
!@D.PossPx@+Der+Der/Dimin+N+Distr+Nom:@D.PossPx@%^Red1CV1toCV2CPL # ;
@R.PossPx@+Der+Der/Dimin+N+Distr+Nom:@R.PossPx@%^Red1CV1toCV2CPL PRE-POSS_01 ;! +Der/Red1_1i13_Distr_1212

! Declension
LEXICON SG-SUF
!@D.PossPx@+Sg+Nom:@D.PossPx@ # ; !CHECKME 2021-07
+Sg+Nom: PRE-POSS_01 ;

LEXICON PL_RED1_qelb
@D.PossPx@+Distr+Nom:@D.PossPx@%^Red1CV2CiCV   # ;
+Distr+Nom:%^Red2CV2CiCV        PRE-POSS_01 ;
!! qəlb+N+Distr+Nom: **rain showers**
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}qəlb%^Red1CV2CiCV
!!€ 00qi0qəlb0

LEXICON PL_RED2
@D.PossPx@+Distr+Nom:@D.PossPx@%^Red2CVC PRE-POSS_01 ;
!@D.PossPx@+Distr+Nom:@D.PossPx@%^Red2CVC # ;
+Distr+Nom:%^Red2CVC	PRE-POSS_01 ;
!! bəlups+N+Distr+Nom
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}bəlups%^Red2CVC
!!€ 00bəlbəlups0
!!$ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}bəlups%^Red2CVC
!!$ 00000bəlups0
!! k̓ʷəčədiʔ+N+Distr+Nom: __rabbit/jänis__
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}k̓ʷəčədiʔ%^Red2CVC
!!€ 00k̓ʷəčk̓ʷəčədiʔ0
!!$ k̓ʷəčədiʔ: k̓ʷəčədiʔ+N+Distr+Nom

LEXICON PL_RED5
!ʔaciɬtalbixʷ:ʔacaciɬtalbixʷ
@D.PossPx@+Distr+Nom:@D.PossPx@%^Red12323 PRE-POSS_01 ;
@R.PossPx@+Distr+Nom:@R.PossPx@%^Red12323	PRE-POSS_01 ;

LEXICON PRE-POSS_01
@R.TAM-SUBJ.SUBJ@+Mod/Subj:@R.TAM-SUBJ.SUBJ@ PRE-POSS_02 ;
@D.TAM-SUBJ@ PRE-POSS_02 ;

LEXICON PRE-POSS_02
@R.TAM-HAB.HAB@+Hab:@R.TAM-HAB.HAB@ PRE-POSS_03 ;
@R.TAM-HAB.IRR@+Irr:@R.TAM-HAB.IRR@ PRE-POSS_03 ;
@D.TAM-HAB@ PRE-POSS_03 ;

LEXICON PRE-POSS_03
@R.TAM-RE.RE@+Anew:@R.TAM-RE.RE@ PRE-POSS_04 ;
@D.TAM-RE.RE@ PRE-POSS_04 ;

LEXICON PRE-POSS_04
@R.TAM-T.REM@+RemPst:@R.TAM-T.REM@ PRE-POSS_05 ;
@D.TAM-T@ PRE-POSS_05 ;

LEXICON PRE-POSS_05
@R.TAM-A.STAT@+Asp/Stat:@R.TAM-A.STAT@ PRE-POSS_06 ;
@R.TAM-A.PROG@+Asp/Prog:@R.TAM-A.PROG@ PRE-POSS_06 ;
@R.TAM-A.PROGSTAT@+Asp/ProgStat:@R.TAM-A.PROGSTAT@ PRE-POSS_06 ;
@R.TAM-A.PFV@+Asp/Pfv:@R.TAM-A.PFV@ PRE-POSS_06 ;
@D.TAM-A@ PRE-POSS_06 ;

LEXICON PRE-POSS_06
@R.NMZ.V2N@+Der/s+N+Sg+Nom:@R.NMZ.V2N@ POSS ;
@R.NMZ.V2A@+Ptc:@R.NMZ.V2A@ POSS ;
@D.NMZ@ POSS ;

LEXICON POSS
@D.PossPx@:@D.PossPx@ K ;
@R.PossPx.Sg1@ PXSG1 ;
@R.PossPx.Sg2@ PXSG2 ;
@D.PossPx@ PXSP3 ;
@D.PossPx@ PXPL1 ;
@D.PossPx@ PXPL2 ;

LEXICON PXSG1
+PxSg1: K ;

LEXICON PXSG2
+PxSg2: K ;

LEXICON PXSP3
+PxSP3:%>s K ;

LEXICON PXPL1
+PxPl1:%>čəɬ K ;

LEXICON PXPL2
+PxPl2:%>ləp K ;
+PxPl2:%>lap K ;

! vim: set ft=xfst-lexc:


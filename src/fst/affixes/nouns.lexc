!! Noun inflection
! ---------------
!! The Lushootseed language nouns inflect in time person aspect.
! s%<ɬalil%>tu%>b%>s%>əxʷ
! VNoun+come.ashore+V+Caus+Mid+Px3+Now
! np+come·ashore+[caus]+[md]+3s+now
! %>tu ~ %>t
! %>əb ~ %>b


LEXICON Cases
+Sg+Nom:     #   ;
+Pl+Nom:i     #   ;
!#:   Nouns   ;

LEXICON N_with-PL_RED1_qelb
!: DIM_CV1toCV2C ; What should this be?
: SG-SUF ;
: PL_RED1_qelb ;

LEXICON N_with-PL_RED2
: DIM_CV1toCV2C ;
: SG-SUF ;
: PL_RED2 ;

LEXICON N-FEM_with-PL_RED2
+Fem: DIM_CV1toCV2C ;
+Fem: SG-SUF ;
+Fem: PL_RED2 ;

LEXICON N-MSC_with-PL_RED2
+Msc: DIM_CV1toCV2C ;
+Msc: SG-SUF ;
+Msc: PL_RED2 ;

! Derivation
LEXICON DIM_CV1toCV2C
@D.PossPx@+Der+Der/Dimin+N+Sg+Nom:@D.PossPx@%^Red1CV1toCV2C # ;
+Der+Der/Dimin+N+Sg+Nom:%^Red1CV1toCV2C POSS ;
@D.PossPx@+Der+Der/Dimin+N+Pl+Nom:@D.PossPx@%^Red1CV1toCV2CPL # ;
+Der+Der/Dimin+N+Pl+Nom:%^Red1CV1toCV2CPL POSS ;

! Declension
LEXICON SG-SUF
@D.PossPx@+Sg+Nom:@D.PossPx@ # ;
+Sg+Nom: POSS ;

LEXICON PL_RED1_qelb
@D.PossPx@+Pl+Nom:@D.PossPx@%^Red1CV2CiCV   # ;
+Pl+Nom:%^Red2CV2CiCV        POSS ;
!! qəlb+N+Pl+Nom: __rain showers__
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}qəlb%^Red1CV2CiCV
!!€ 00qi0qəlb0
LEXICON PL_RED2
@D.PossPx@+Pl+Nom:@D.PossPx@%^Red2CVC2CVCCVC # ;
+Pl+Nom:%^Red2CVC2CVCCVC	POSS ;
!! bəlups+N+Pl+Nom
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}bəlups%^Red2CVC2CVCCVC
!!€ 00bəlbəlups0
!!$ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}bəlups%^Red2CVC2CVCCVC
!!$ 00000bəlups0
!!€ k̓ʷəčk̓ʷəčədiʔ: k̓ʷəčədiʔ+N+Pl+Nom
!!$ k̓ʷəčədiʔ: k̓ʷəčədiʔ+N+Pl+Nom

LEXICON POSS
@R.PossPx.Sg1@ PXSG1 ;
@R.PossPx.Sg2@ PXSG2 ;
@U.PossPx.SP3@ PXSP3 ;
@U.PossPx.Pl1@ PXPL1 ;
@U.PossPx.Pl2@ PXPL2 ;

LEXICON PXSG1
+PxSg1: K ;

LEXICON PXSG2
+PxSg2: K ;

LEXICON PXSP3
+PxSP3:%>s K ;

LEXICON PXPL1
+PxPl1:%>čəɬ K ;

LEXICON PXPL2
+PxPl2:%>ləp K ;
+PxPl2:%>lap K ;

! vim: set ft=xfst-lexc:


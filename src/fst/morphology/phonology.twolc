!! =================================== !
!! The Lushootseed morphophonological/twolc rules file !
!! =================================== !

Alphabet
 a b c d g h i k l m n o p q r s t u v w x y z
 é ó é ò â ê ô û î ã ý


!! The Lushootseed language is written in lower case only
 A B C D G H I K L M N O P Q R S T U V W X Y Z
 Á É Ó Ú Í À È Ò Ù Ì Â Ê Ô Û Î Ã Ý

!! This transformation, accent loss, will be moved to post dict. 2022-12-25
á:a
ú:u
í:i
ə́:ə
à:a
ù:u
ì:i
ə̀:ə

b̓

c̓ !! c U+0313
č
č̓
dᶻ 
ə
gʷ 
ǰ

k̓ 
kʷ 
k̓ʷ 

l̕ 
ɬ
ƛ̕

m̓
n̓

p̓

q
q̓
qʷ
q̓ʷ

š

t̕

w̓
y̓

x̌
xʷ
x̌ʷ

ʔ
!! Lushootseed is written in IPA, there are no upper-case letters


%{p5%}:b
%{p5%}:b̓
%{p5%}:c
%{p5%}:c̓
%{p5%}:č
%{p5%}:č̓
%{p5%}:d
%{p5%}:dᶻ
%{p5%}:g
%{p5%}:gʷ
%{p5%}:ǰ
%{p5%}:k
%{p5%}:k̓
%{p5%}:kʷ
%{p5%}:k̓ʷ
%{p5%}:l
%{p5%}:l̕
%{p5%}:ɬ
%{p5%}:ƛ̕
%{p5%}:m
%{p5%}:m̓
%{p5%}:n
%{p5%}:n̓
%{p5%}:p
%{p5%}:p̓
%{p5%}:q
%{p5%}:q̓
%{p5%}:qʷ
%{p5%}:q̓ʷ
%{p5%}:š
%{p5%}:t
%{p5%}:t̕
%{p5%}:w
%{p5%}:w̓
%{p5%}:y
%{p5%}:y̓
%{p5%}:x̌
%{p5%}:xʷ
%{p5%}:x̌ʷ
%{p5%}:ʔ

%{p4%}:a
%{p4%}:i
!%{p4%}:o
%{p4%}:u
!%{p4%}:á
!%{p4%}:ú
!%{p4%}:í
!%{p4%}:ə́
%{p4%}:ə


%{p3%}:b
%{p3%}:b̓
%{p3%}:c
%{p3%}:c̓
%{p3%}:č
%{p3%}:č̓
%{p3%}:d
%{p3%}:dᶻ
%{p3%}:g
%{p3%}:gʷ
%{p3%}:ǰ
%{p3%}:k
%{p3%}:k̓
%{p3%}:kʷ
%{p3%}:k̓ʷ
%{p3%}:l
%{p3%}:l̕
%{p3%}:ɬ
%{p3%}:ƛ̕
%{p3%}:m
%{p3%}:m̓
%{p3%}:n
%{p3%}:n̓
%{p3%}:p
%{p3%}:p̓
%{p3%}:q
%{p3%}:q̓
%{p3%}:qʷ
%{p3%}:q̓ʷ
%{p3%}:š
%{p3%}:t
%{p3%}:t̕
%{p3%}:w
%{p3%}:w̓
%{p3%}:y
%{p3%}:y̓
%{p3%}:x̌
%{p3%}:xʷ
%{p3%}:x̌ʷ
%{p3%}:ʔ

%{p2%}:a
%{p2%}:i
!%{p2%}:o
%{p2%}:u
!%{p2%}:á
!%{p2%}:ú
!%{p2%}:í
!%{p2%}:ə́
%{p2%}:ə


%{p1%}:b
%{p1%}:b̓
%{p1%}:c
%{p1%}:c̓
%{p1%}:č
%{p1%}:č̓
%{p1%}:d
%{p1%}:dᶻ
%{p1%}:g
%{p1%}:gʷ
%{p1%}:ǰ
%{p1%}:k
%{p1%}:k̓
%{p1%}:kʷ
%{p1%}:k̓ʷ
%{p1%}:l
%{p1%}:l̕
%{p1%}:ɬ
%{p1%}:ƛ̕
%{p1%}:m
%{p1%}:m̓
%{p1%}:n
%{p1%}:n̓
%{p1%}:p
%{p1%}:p̓
%{p1%}:q
%{p1%}:q̓
%{p1%}:qʷ
%{p1%}:q̓ʷ
%{p1%}:š
%{p1%}:t
%{p1%}:t̕
%{p1%}:w
%{p1%}:w̓
%{p1%}:y
%{p1%}:y̓
%{p1%}:x̌
%{p1%}:xʷ
%{p1%}:x̌ʷ
%{p1%}:ʔ

%{pØ%}:a
%{pØ%}:i
!%{pØ%}:o
%{pØ%}:u
!%{pØ%}:á
!%{pØ%}:ú
!%{pØ%}:í
!%{pØ%}:ə́
%{pØ%}:ə



! 3-segment reduplication
R31:b R31:l

R32:l
E3:ə
!! place holders
%{pØ%}:0
%{p1%}:0
%{p2%}:0
%{p3%}:0
%{p4%}:0
%{p5%}:0
%{p6%}:0
%{p7%}:0

! triggers
%^Red1i123:0 !!= * @CODE@ qəlb: qiqəlb **rain showers**
%^Red1i13:0 !!= * @CODE@ 2-segment to 3-segments with vowel change
%^Red12q13:0 !!= * @CODE@
%^Red1213:0 !!= * @CODE@
%^Red1iq13:0 !!= * @CODE@ iʔ
%^Red1iq123:0 !!= * @CODE@ iʔ biʔbədəʔs
%^Red12123:0 !!= * @CODE@ papastəd
%^RedPL1i1i13:0 !!= * @CODE@ 2-segment to 3-segments with vowel change Plural
%^Red12323:0 !!= * @CODE@ works with Red3 and Red5

%^RedPL123123:0 !!= * @CODE@ 3-segment reduplication 

%^RedPL130130:0 !!= * @CODE@ ƛ̕xʷay: ƛ̕xʷaƛ̕xʷay 

%^Pen:0 !!= * @CODE@ this combines with subsequent RmVow or perhaps RmCns
%^RmVOW:0 !!= * @CODE@ this removes previous vowel or with Pen the penultimate vowel
%^d2t:0 !!= * @CODE@ this devoices d to t specifically



! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds

 ! Escaped chars
 %  

        !! ## Symbols that need to be escaped on the lower side (towards twolc):
 »7     !!≈ * **@CODE@**:  Literal »
 «7     !!≈ * **@CODE@**:  Literal «
        !! ```
 %[%>%] !!= @CODE@ - Literal >
 %[%<%] !!= @CODE@ - Literal <
        !! ```
%^√:0  !!≈ * **@CODE@**: at commence of root


%- !!≈ * **@CODE@**:  hyphen

;


Sets

 Vow = a e i o u y ə
       á é ó ú í à è ò ù ì â ê ô û î ã ý ;

 CnsNoGlotOrD = b c f g h j k l m n p q r s t v w x y z 
š ǰ  č
c̓
č̓
dᶻ
gʷ
ɬ
l
l̕
ƛ̕
w̓
t̕
y̓
p̓
k̓
kʷ
k̓ʷ
q
qʷ
q̓
q̓ʷ
xʷ
x̌
x̌ʷ
b̓
n̓
m̓

ɬ
ƛ̕


;

Cns = CnsNoGlotOrD ʔ d ;

Lts = Cns Vow ;


!! Reduplications are worked with on the basis of the Lushootseed Dictionary by Dawn Bates, Thom Hess and Vi Hilbert. Seattle & London. 1994:xvii

!! Red1
!! C V1 => C V1 C V1 
!!€ s%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}tubš
!!€ s000tu0tubš 
!!€ ʔiɬ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}t̕isu
!!€ ʔiɬ000t̕i0t̕isu

!! C V1 => C V1 C ə  
!!€ s%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}tuləkʷ
!!€ s000tu0tələkʷ
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}yubil
!!€ 00yu0yəbil
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}ɬaɬil
!!€ 00ɬa0ɬəlil
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}k̓ʷid
!!€ 00k̓ʷi0k̓ʷəd

!! C V1 => C V1 C   
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}pastəd
!!€ 00pa0p0stəd
!!€ s%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}x̌ʷiƛ̕əyʔ 
!!€ s000x̌ʷi0x̌ʷ0ƛ̕əyʔ

!! C ə => C i C   %^Red1i13:
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}ʔəpus%^Red1i13
!!€ 00ʔi0ʔ0pus0

!! C ə => C i C ə
!! Red 1 qəlb: qiqəlb %^Red1i123
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}qəlb%^Red1i123
!!€ 00qi0qəlb0

Rules


! Vowels
"a:0 in inflection"
!! **@RULENAME@**
 a:0 <=>
        _ d: %^Pen: %^RmVOW: %^d2t: %>  ; !-t-əb Passive
    %{p1%}: (%{pØ%}:) [%^Red1i13:|%^RedPL1i1i13:|%^Red1iq13:|%^Red12q13:|%^Red1213:] Cns _ [ Cns | Vow ] [ Lts: | » ]* ;
    
"ə:0 in inflection"
!! **@RULENAME@**
 ə:0 <=>  _ d: %> c: Vow ;!CHECKME
!!€ x̌əd»əd%>sid
!!€ x̌əd000%>cid
        _ d: %^Pen: %^RmVOW: %^d2t: %>  ; !-t-əb Passive
!echo 'b ə q̓ ə:0 d:t ^Pen:0 ^RmVOW:0 ^d2t:0 > ə b' | hfst-pair-test src/fst/phonology.compose.hfst
!!€ bəq̓əd^Pen^RmVOW^d2t>əb
!!€ bəq̓0t000>əb

    %{p1%}: (%{pØ%}:) [%^Red1i13:|%^RedPL1i1i13:|%^Red1iq13:|%^Red12q13:|%^Red1213:] Cns _ [ Cns | Vow ] [ Lts: | » ]* ;
!! sčətxʷəd+N+Der/Dimin+N+Pl __bear/karhu__
!!€ s%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}čətxʷəd%^RedPL1i1i13
!!€ s%^√čiči0č0txʷəd0

"i:0 in inflection"
!! **@RULENAME@**
 i:0 <=>  %{p1%}: (%{pØ%}:) [%^Red1i13:|%^RedPL1i1i13:|%^Red1iq13:|%^Red12q13:|%^Red1213:] Cns _ [ Cns | Vow ] [ Lts: | » ]* ;

"í:0 in inflection"
!! **@RULENAME@**
 í:0 <=>  %{p1%}: (%{pØ%}:) [%^Red1i13:|%^RedPL1i1i13:|%^Red1iq13:|%^Red12q13:|%^Red1213:] Cns _ [ Cns | Vow ] [ Lts: | » ]* ;

! Vowels
"ʔ:0 in preceding s"
!! **@RULENAME@**
 ʔ:0 <=> s [%< | %^√: ] _ u %< ;
!! ʔəɬəd+V+Prf+Der+Der/N+N+Sg+PxSg2: __eat/syödä__
!!€ ad<s<ʔu<ʔəɬəd
!!€ ad<s<0u<ʔəɬəd
      [ ƛ̕ | t ] u: %< _ a s %< ;
 %{p1%}: (%{pØ%}:) %^Red12323: _ Vow: [ Lts | » ]* ; ! see Cns:0 in reduplication 3 and 5
!!

"u:0 in preceding ƛ̕"
!! **@RULENAME@**
 u:0 <=> [ ƛ̕ | t ] _ %< ʔ: a s ;


!"ə:i in reduplication"
!!! __@RULENAME@__
! ə:i <=>
!         [ # | %^√:0 | %< ] Cns _ 0: [ Cns | Vow ] [ Lts: | » ]* %^Red1i13: ;
!         [ # | %^√:0 | %< ] 0: 0: Cns _ 0: [ Cns | Vow ] [ Lts: | » ]* %^RedPL1i1i13: ;

"%{pØ%}:a in reduplication"
!! **@RULENAME@**
 %{pØ%}:a <=> %{p3%}: %{p2%}: %{p1%}: _ [%^RedPL130130:] Cns: Cns: [a|á:] [ Lts: | » ]* ;




"%{p2%}:ə in reduplication"
!! **@RULENAME@**
 %{p2%}:ə <=> %{p3%}: _ %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^Red12123:|%^Red12q13:|%^Red1213:] Cns: ə Cns: [ Lts: | » ]* ;
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}bəlups%^RedPL123123
!!€ 00bəlbəlups0

"%{p2%}:u in reduplication"
!! **@RULENAME@**
 %{p2%}:u <=> %{p3%}: _ %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^Red12123:|%^Red12q13:|%^Red1213:] Cns: u Cns: [ Lts: | » ]* ;


"%{p2%}:í in reduplication"
!! **@RULENAME@**
 %{p2%}:í <=>  _ %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^Red12123:|%^Red12q13:|%^Red1213:] Cns: í: Cns: [ Lts: | » ]* ;

"%{p2%}:i in reduplication"
!! **@RULENAME@**
 %{p2%}:i <=>  _ %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^RedPL1i1i13:|%^Red1i13:|%^Red1iq13:|%^Red1iq123:|%^Red12123:|%^Red12q13:|%^Red1213:] Cns: i: Cns: [ Lts: | » ]* ;
               _ %{p1%}: (%{pØ%}:) [%^RedPL1i1i13:|%^Red1i13:|%^Red1iq13:|%^Red1iq123:] Cns: ə: Cns: [ Lts: | » ]* ;
               _ %{p1%}: (%{pØ%}:) [%^Red1i123:|%^Red1iq13:|%^Red1iq123:] Cns: ə Cns: [ Lts: | » ]* ; 
               _ %{p1%}: (%{pØ%}:) [%^Red1i123:|%^Red1iq13:|%^Red1iq123:|%^Red1i13:|%^RedPL1i1i13:] Cns: [a:|u:|i:|á:|à:|í:|ì:|ú:|ù:] Cns: [ Lts: | » ]* ; 

!!€ {p5}{p4}{p3}{p2}{p1}x̌aʔx̌aʔ^Red1iq13
!!€ 00x̌iʔx̌0ʔx̌aʔ0
!! qəlb+N+Pl: **rain showers**
!!€ {p5}{p4}{p3}{p2}{p1}qəlb^Red1i123
!!€ 00qi0qəlb0
!! pišpiš+N+Pl: **cat/kissa**
!!€ {p5}{p4}{p3}{p2}{p1}pišpiš^RedPL123123
!!€ 00pišpišpiš0

"%{p4%}:i in reduplication"
!! **@RULENAME@**
 %{p4%}:i <=> _ %{p3%}:  %{p2%}:  %{p1%}: (%{pØ%}:) %^RedPL1i1i13: Cns: ə: Cns: [ Cns | Vow ] [ Lts: | » ]* ;
              _ %{p3%}:  %{p2%}:  %{p1%}: (%{pØ%}:) %^RedPL1i1i13: Cns: [i:|í:] Cns: [ Cns | Vow ] [ Lts: | » ]* ;

"%{p2%}:a in reduplication"
!! **@RULENAME@**
 %{p2%}:a <=> %{p3%}: _ %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^Red12123:|%^Red12q13:|%^Red1213:] Cns:  a  Cns: [ Lts: | » ]* ;

"%{p2%}:á in reduplication"
!! **@RULENAME@**
 %{p2%}:á <=> %{p3%}: _ %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^Red12123:|%^Red12q13:|%^Red1213:] Cns:  á:  Cns: [ Lts: | » ]* ;

"%{pØ%}:0"
!! **@RULENAME@**
 %{pØ%}:0 <=> %{p1%}: _ \[ %^RedPL130130:] [ Lts: | » ]* [ # | %>: ] ;


"%{p1%}:0"
!! **@RULENAME@**
 %{p1%}:0 <=> _ (%{pØ%}:) (%^RedPL1i1i13: | %^Red1i13: |%^Red1213:|%^Red12123:) [ Lts: | » ]+ [  # | %>: ] ;

"%{p2%}:0"
!! **@RULENAME@**
 %{p2%}:0 <=> _ %{p1%}: (%{pØ%}:) \[ %^RedPL1i1i13: | %^Red1i13: | %^RedPL123123: | %^Red12323: |%^Red12q13:|%^Red1213:| %^Red1i123: |%^Red12123:| %^Red1iq13:|%^Red1iq123:] [ Lts: | » ]* [ # | %>: ] ;
! _ %{p1%}: [ Lts: | » ]* \[ %^RedPL1i1i13: | %^Red1i13: | %^RedPL123123: | %^Red12323: | %^Red1i123: |%^Red12123:| %^Red1iq13:|%^Red1iq123:] [ # | %>: ] ;
            _ %{p1%}: %{pØ%}: %^RedPL130130: Cns: Cns: Vow: [ Lts: | » ]* ;   

"%{p3%}:0"
!! **@RULENAME@**
 %{p3%}:0 <=> _ %{p2%}:  %{p1%}: (%{pØ%}:) \[ %^RedPL1i1i13: | %^Red1i13: | %^RedPL123123: | %^Red12323: | %^Red1i123: |%^Red1iq13:|%^Red1iq123:|%^Red12123:|%^RedPL130130:] [ Lts: | » ]* [ # | %>: ] ;

"%{p4%}:0"
!! **@RULENAME@**
 %{p4%}:0 <=> _ %{p3%}:  %{p2%}:  %{p1%}: (%{pØ%}:) (%^RedPL123123: | %^Red12323: | %^Red1i13: | %^Red1iq13:|%^Red1iq123:|%^Red12123:|%^Red12q13:|%^Red1213:|%^RedPL130130:) [ Lts: | » ]*  [  # | %>: ] ;

"%{p5%}:0"
!! **@RULENAME@**
 %{p5%}:0 <=> _ %{p4%}: %{p3%}:  %{p2%}:  %{p1%}: (%{pØ%}:) (%^RedPL123123: | %^Red12323: | %^Red1i13: |%^Red1iq13:|%^Red1iq123:|%^Red12123:|%^Red12q13:|%^Red1213:|%^RedPL130130:) [ Lts: | » ]* [ # | %>: ];


!!! ### CONSONANTS
!"s:c after d:0"
!!! **@RULENAME@**
!s:c <=> d: %> _ # ;

!!€ ʔu%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}kʷədad%>s
!!€ ʔu000000kʷəda0%>c

"d:0 before s:c"
!! **@RULENAME@**
d:0 <=>	_ %> [ c ]  ;
 %{p1%}: (%{pØ%}:) %^Red12323: _ Vow: [ Lts | » ]* ; ! see Cns:0 in reduplication 3 and 5
!d:0 <=>	_ %> [ s: #| c ]  ;
!!!€ ʔu%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}kʷədad%>c
!!!€ ʔu000000kʷəda0%>c
!!€ ʔu%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}kʷədad%>cid
!!€ ʔu000000kʷəda0%>cid

"d:t before ə b"
!! **@RULENAME@**
d:t <=>	[ə:|»|i:|a:|u:] _ %^Pen: %^RmVOW: %^d2t: %> ə b  ;
!echo 'b ə q̓ ə:0 d:t ^Pen:0 ^RmVOW:0 ^d2t:0 > ə b' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'bəq̓əd+V+Pass' | hfst-lookup src/fst/lexicon.hfst
!!€ bəq̓əd^Pen^RmVOW^d2t>əb
!!€ bəq̓0t000>əb



"%{p1%}:Cx in reduplication"
!! **@RULENAME@**
 %{p1%}:Cx <=> %{p3%}: %{p2%}: _ (%{pØ%}:) [%^RedPL123123:|%^Red12323:|%^RedPL130130:] Cns: (Vow:) Cx [ Lts | » ]* ;
           where Cx in (kʷ b̓ ǰ n̓ m̓ p p̓ b t t̕ d dᶻ c c̓ č č̓ s š  ɬ ƛ̕  k k̓ k̓ʷ gʷ h xʷ q q̓ qʷ  x̌ x̌ʷ ) ;
!!2017-11-27where Cx in (p b t t̕ d dᶻ č š l l̕ ɬ ƛ̕ w w̓ y̓ k̓ʷ gʷ q q̓ qʷ q̓ʷ x̌ x̌ʷ ʔ) ;

"%{p3%}:Cx in reduplication"
!! **@RULENAME@**
 %{p3%}:Cx <=> _ %{p2%}: %{p1%}: (%{pØ%}:) [%^RedPL123123:|%^RedPL130130:] Cx (Vow:) Cns [ Lts | » ]* ;
                _ %{p2%}: %{p1%}: (%{pØ%}:) %^Red12323: Cx: Vow: Cns [ Lts | » ]* ;
               _ %{p2%}: %{p1%}: (%{pØ%}:) [ %^Red1i13:|%^RedPL1i1i13:|%^Red1iq13:|%^Red1iq123:] Cx Vow: Cns [ Lts | » ]* ;
               _ %{p2%}: %{p1%}: (%{pØ%}:) [ %^Red1i123:|%^Red12123:] Cx Vow Cns [ Lts | » ]* ;
           where Cx in (kʷ b̓ ǰ n̓ m̓ p p̓ b t t̕ d dᶻ c c̓ č č̓ s š l l̕ ɬ ƛ̕  y w y̓ w̓ k k̓ k̓ʷ gʷ h xʷ q q̓ qʷ q̓ʷ x̌ x̌ʷ ʔ) ;
!2019-03-04           where Cx in (p p̓ b t t̕ d dᶻ c c̓ č č̓ s š l l̕ ɬ ƛ̕ y w y̓ w̓ k k̓ k̓ʷ gʷ h xʷ q q̓ qʷ q̓ʷ x̌ x̌ʷ ʔ) ;

"%{p5%}:Cx in reduplication"
!! **@RULENAME@**
 %{p5%}:Cx <=>
               _ %{p4%}: %{p3%}: %{p2%}: %{p1%}: (%{pØ%}:)%^RedPL1i1i13:  Cx Vow: Cns [ Lts | » ]* ;
           where Cx in (kʷ b̓ ǰ n̓ m̓ p p̓ b t t̕ d dᶻ c c̓ č č̓ s š l l̕ ɬ ƛ̕  y w y̓ w̓ k k̓ k̓ʷ gʷ h xʷ q q̓ qʷ q̓ʷ x̌ x̌ʷ ʔ) ;
!2019-03-04           where Cx in (p p̓ b t t̕ d dᶻ c c̓ č č̓ s š l l̕ ɬ ƛ̕ y w y̓ w̓ k k̓ k̓ʷ gʷ h xʷ q q̓ qʷ q̓ʷ x̌ x̌ʷ ʔ) ;
!! sčətxʷəd+N+Der/Dimin+N+Pl bear
!!€ s%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}čətxʷəd%^RedPL1i1i13
!!€ s%^√čičič0txʷəd0

!! liquids

"%{p1%}:l̕ in reduplication"
!! **@RULENAME@**
 %{p1%}:l̕ <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: [q|y] Vow: l [ Lts | » ]* ;
             %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: Cns Vow: l̕ [ Lts | » ]* ;

"%{p1%}:y̓ in reduplication"
!! **@RULENAME@**
 %{p1%}:y̓ <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: q Vow: y [ Lts | » ]* ;
             %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: Cns Vow: y̓ [ Lts | » ]* ;



"%{p1%}:w̓ in reduplication"
!! **@RULENAME@**
 %{p1%}:w̓ <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: q Vow: w [ Lts | » ]* ;
             %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: Cns Vow: w̓ [ Lts | » ]* ;

"%{p1%}:ʔ in reduplication"
!! **@RULENAME@**
 %{p1%}:ʔ <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: s Vow: q̓ʷ [ Lts | » ]* ;
             %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: Cns Vow: ʔ [ Lts | » ]* ;
             %{p3%}: %{p2%}: _ (%{pØ%}:) [%^Red1iq13:|%^Red1iq123:] Cns Vow: Cns: [ Lts | » ]* ;

!! x̌aʔx̌aʔ+N+Der+Der/Dimin+N+Sg: **sacred/pyhä**
!!€ {p5}{p4}{p3}̌{p2}{p1}x̌aʔx̌aʔ^Red1iq123
!!€ 00x̌iqx̌aʔx̌aʔ0

"%{p1%}:Cx in reduplication"
!! **@RULENAME@**
 %{p1%}:Cx <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: [Cns - [q|y]] Vow: Cx [ Lts | » ]* ;
  %{p3%}: %{p2%}: _ (%{pØ%}:) %^Red12323: Cns: Vow: Cx [ Lts | » ]* ;
           where Cx in (l) ;
!!€ %{p5%}%{p4%}%{p3%}%{p2%}%{p1%}bəlups%^RedPL123123
!!€ 00bəlbəlups0
	   
"%{p1%}:Cx in reduplication"
!! **@RULENAME@**
 %{p1%}:Cx <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: [Cns - q] Vow: Cx [ Lts | » ]* ;
  %{p3%}: %{p2%}: _ (%{pØ%}:) %^Red12323: Cns: Vow: Cx [ Lts | » ]* ;
           where Cx in (y w) ;

"%{p1%}:Cx in reduplication"
!! **@RULENAME@**
 %{p1%}:Cx <=> %{p3%}: %{p2%}: _ (%{pØ%}:) %^RedPL123123: [Cns - s] Vow: Cx [ Lts | » ]* ;
  %{p3%}: %{p2%}: _ (%{pØ%}:) %^Red12323: Cns: Vow: Cx [ Lts | » ]* ;
           where Cx in (q̓ʷ) ;

"Cns:0 in reduplication 3 and 5"
!! **@RULENAME@**
CnsNoGlotOrD:0 <=> %{p1%}: (%{pØ%}:) %^Red12323: _ Vow: Cns: [ Lts | » ]* ;
!{p5}{p4}{p3}{p2}{p1}ʔaciɬtalbixʷ^Red12323
!00ʔac0aciɬtalbixʷ0


!"Disallow full vowel removal with Red1iq13 type of reduplication"
!!! **@RULENAME@**
!13:0 /<=
!  %{p1%}: (%{pØ%}:) Cns [a: | á: | à: | i: | í: | ì: | u: | ú: | ù:]  [ Lts | » ]* _ [ %> | # ]; ! full vowel not lost
!  where 13 in (%^RedPL1i1i13 %^Red1i13 %^Red12q13 %^Red1iq13 %^RedPL130130);



!!TESTS
!echo 's ^√:0 {p5}:č {p4}:i {p3}:č {p2}:i {p1}:0 ^Red1CV1toCV2CPL:0 č ə:0 t xʷ ə d' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'sčətxʷəd+N+Der+Der/Dimin+N+Pl' | hfst-lookup src/fst/lexicon.hfst

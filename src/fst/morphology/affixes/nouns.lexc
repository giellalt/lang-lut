!! Noun inflection
! ---------------
!! The Lushootseed language nouns inflect in time person aspect.
! s%<ɬalil%>tu%>b%>s%>əxʷ
! VNoun+come.ashore+V+Caus+Mid+Px3+Now
! np+come·ashore+[caus]+[md]+3s+now
! %>tu ~ %>t
! %>əb ~ %>b


LEXICON N_with_PL_RED1_qelb
!LEXICON N_with-PL_RED1_qelb
!: DIM_CV1toCV2C ; What should this be?
@U.REDUPL.ZERO@:@U.REDUPL.ZERO@ SG-SUF ;
: PL_RED1_qelb ;



LEXICON N_with_PL_RED2_Dial/-SL
+Dial/-SL: N_with_PL_RED2 ;
LEXICON N_with_PL_RED2_PRF/s_Dial/-SL
 N_with_PL_RED2_PRF/s ;

LEXICON N_with_PL_RED2_Dial/-NL
+Dial/-NL: N_with_PL_RED2 ;

LEXICON N_with_PL_RED2_0/l_Dial/-NL
+Dial/-NL: N_with_PL_RED2_0/l ;

LEXICON N_with_PL_RED2_0/l !!fixme 2024-06-08
: N_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/je_Dial/-NL
+Dial/-NL: N_with_PL_RED2_PRF/je ;

LEXICON N_with_PL_RED2_PRF/s_Dial/-NL
+Dial/-NL: N_with_PL_RED2_PRF/s ;

LEXICON N_with_PL_RED2_PRF/c
@U.NPrefix.c@:@U.NPrefix.c@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/dexw
@U.NPrefix.dexw@:@U.NPrefix.dexw@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/dxws
@U.NPrefix.dxws@:@U.NPrefix.dxws@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/dxw
@U.NPrefix.dxw@:@U.NPrefix.dxw@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/je
@U.NPrefix.je@:@U.NPrefix.je@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/le
@U.NPrefix.le@:@U.NPrefix.le@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/s
@U.NPrefix.s@:@U.NPrefix.s@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/sexw
@U.NPrefix.sexw@:@U.NPrefix.sexw@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/sxw
@U.NPrefix.sxw@:@U.NPrefix.sxw@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2_PRF/xh
@U.NPrefix.xh@:@U.NPrefix.xh@ N0_with_PL_RED2 ;

LEXICON N_with_PL_RED2 !This is the typical noun with plural reduplication 123123 
@U.NPrefix.ZERO@:@U.NPrefix.ZERO@ N0_with_PL_RED2 ;


LEXICON N0_with_PL_RED2
@U.NMZ.ZERO@ N1_with_PL_RED2 ;

LEXICON N1_with_PL_RED2
!2024-04-12: DIM_CV1toCV2C ;
@U.REDUPL.ZERO@:@U.REDUPL.ZERO@ SG-SUF ;
: PL_RED2 ;

LEXICON N_with_PL_RED130
!%^RedPL130130
@U.REDUPL.ZERO@:@U.REDUPL.ZERO@ SG-SUF ;
@U.PossPx.ZERO@@U.REDUPL.130130@+Pl:@U.PossPx.ZERO@@U.REDUPL.130130@ PRE-POSS_01 ;
@U.REDUPL.130130@+Pl:@U.REDUPL.130130@	PRE-POSS_01 ;


LEXICON N_with_Distr_12323_RED5 !Der/Red5_12323
!%^Red12323
!: DIM_CV1toCV2C ;
@U.REDUPL.ZERO@:@U.REDUPL.ZERO@ SG-SUF ;
: PL_RED5 ;

LEXICON N_FEM_with_PL_RED2
!LEXICON N-FEM_with-PL_RED2
!+Fem: DIM_CV1toCV2C ;
!+Fem: SG-SUF ;
!+Fem: PL_RED2 ;
: DIM_CV1toCV2C ;
@U.REDUPL.ZERO@:@U.REDUPL.ZERO@ SG-SUF ;
: PL_RED2 ;

LEXICON N_MSC_with_PL_RED2 !this is simply for retaining information in the lexicon name for deriving CG sets 202602
N_with_PL_RED2 ;
LEXICON N_MSC_with_PL_RED2_PRF/s
@U.NPrefix.s@ DIM_CV1toCV2C ;
@U.NPrefix.s@@U.REDUPL.ZERO@:@U.NPrefix.s@@U.REDUPL.ZERO@ SG-SUF ;
@U.NPrefix.s@ PL_RED2 ;
!+Msc: DIM_CV1toCV2C ;
!+Msc: SG-SUF ;
!+Msc: PL_RED2 ;

! Derivation
LEXICON DIM_CV1toCV2C
@U.REDUPL.1i13@+Der+Der/Dimin_1i13+N:@U.REDUPL.1i13@ ; !+Der/Red1_1i13
@U.REDUPL.1iq13@+Err/Orth+Der+Der/Dimin_1iq13+N:@U.REDUPL.1iq13@ SG-SUF ; !+Der/Red1_1iq13

@U.REDUPL.1iq13@+Err/Orth+Der+Der/Dimin_1iq123+N:@U.REDUPL.1iq13@ SG-SUF ; !+Der/Red1_1iq123

@U.REDUPL.1i123@+Err/Orth+Der+Der/Dimin_1i123+N:@U.REDUPL.1i123@ SG-SUF ; !+Der/Red1_1i123
@U.REDUPL.12123@+Err/Orth+Der+Der/Dimin_12123+N:@U.REDUPL.12123@ ; !+Der/Red1_12123

@U.REDUPL.PL1i1i13@+Der+Der/Dimin_1i1i13+N+Pl:@U.REDUPL.PL1i1i13@ PRE-POSS_01 ;! +Der/Red1_1i13_Distr_1212

LEXICON N_DIM_with_PL_12123_PRF/sexw !bibədaʔ, bibibədaʔ 
@U.NPrefix.sexw@@U.REDUPL.ZERO@+Dimin:@U.NPrefix.sexw@@U.REDUPL.ZERO@ SG-SUF ;
@U.NPrefix.sexw@@U.REDUPL.12123@+Dimin+Pl:@U.NPrefix.sexw@@U.REDUPL.12123@ PRE-POSS_01 ; 

LEXICON N_DIM_with_PL_12123 !bibədaʔ, bibibədaʔ
@U.REDUPL.ZERO@+Dimin:@U.REDUPL.ZERO@ SG-SUF ;
@U.REDUPL.12123@+Dimin+Pl:@U.REDUPL.12123@ PRE-POSS_01 ;

! Declension
LEXICON SG-SUF
+Sg: PRE-POSS_01 ;


LEXICON PL_RED1_qelb
!@U.PossPx.ZERO@+Pl:@U.PossPx.ZERO@%^Red1i123   # ;   
@U.PossPx.ZERO@@U.REDUPL.1i123@+Pl:@U.PossPx.ZERO@@U.REDUPL.1i123@   # ; !+Der/Red1_1i123   

!+Pl:%^Red2CV2CiCV        PRE-POSS_01 ;
@U.REDUPL.2CV2CiCV@+Pl:@U.REDUPL.2CV2CiCV@        PRE-POSS_01 ;!ADD info
!! qəlb+N+Pl: **rain showers**
!!€ {p5}{p4}{p3}{p2}{p1}qəlb^Red1i123
!!€ 00qi0qəlb0

LEXICON PL_RED2 !! where reduplication happens
@U.REDUPL.PL123123@+Pl:@U.REDUPL.PL123123@ PRE-POSS_01 ;
!! bəlups+N+Pl
!!€ {p5}{p4}{p3}{p2}{p1}bəlups^RedPL123123
!!€ 00bəlbəlups0
!!$ {p5}{p4}{p3}{p2}{p1}bəlups^RedPL123123
!!$ 00000bəlups0
!! k̓ʷəčədiʔ+N+Pl: __rabbit/jänis__
!!€ {p5}{p4}{p3}{p2}{p1}k̓ʷəčədiʔ^RedPL123123
!!€ 00k̓ʷəčk̓ʷəčədiʔ0
!!$ k̓ʷəčədiʔ: k̓ʷəčədiʔ+N+Pl

LEXICON PL_RED5
!ʔaciɬtalbixʷ:ʔacaciɬtalbixʷ
@U.REDUPL.12323@+Pl:@U.REDUPL.12323@ PRE-POSS_01 ;

LEXICON PRE-POSS_01
@U.TAM-SUBJ.SUBJ@+Mod/Subj:@U.TAM-SUBJ.SUBJ@ PRE-POSS_02 ; 
@U.TAM-SUBJ.ZERO@ PRE-POSS_02 ; 

LEXICON PRE-POSS_02
@U.TAM-HAB.HAB@+Hab:@U.TAM-HAB.HAB@ PRE-POSS_03 ;
@U.TAM-HAB.IRR@+Irr:@U.TAM-HAB.IRR@ PRE-POSS_03 ; 
@U.TAM-HAB.ZERO@ PRE-POSS_03 ;  

LEXICON PRE-POSS_03
@U.TAM-RE.RE@+Anew:@U.TAM-RE.RE@ PRE-POSS_04 ; 
@U.TAM-RE.ZERO@ PRE-POSS_04 ; 

LEXICON PRE-POSS_04
@R.TAM-T.REM@+RemPst:@R.TAM-T.REM@ PRE-POSS_05 ;
@U.TAM-T.ZERO@ PRE-POSS_05 ;

LEXICON PRE-POSS_05
@U.TAM-A.STAT@+Asp/Stat:@U.TAM-A.STAT@ PRE-POSS_06 ;
@U.TAM-A.PROG@+Asp/Prog:@U.TAM-A.PROG@ PRE-POSS_06 ;
@U.TAM-A.PROGSTAT@+Asp/ProgStat:@U.TAM-A.PROGSTAT@ PRE-POSS_06 ;
@U.TAM-A.PFV@+Asp/Pfv:@U.TAM-A.PFV@ PRE-POSS_06 ;
@U.TAM-A.ZERO@ PRE-POSS_06 ;!2025-06-16 ? Asp/None?

LEXICON PRE-POSS_06
@U.NMZ.V2N@+Der/s+N+Sg:@U.NMZ.V2N@ POSS ;
@U.NMZ.V2A@+Ptc:@U.NMZ.V2A@ POSS ;
@U.NMZ.ZERO@ POSS ;

LEXICON POSS
@U.PossPx.ZERO@:@U.PossPx.ZERO@ K ;   
@U.PossPx.Sg1@ PXSG1 ;
@U.PossPx.Sg2@ PXSG2 ;
@U.PossPx.SP3@ PXSP3 ;
@U.PossPx.Pl1@ PXPL1 ; 
@U.PossPx.Pl2@ PXPL2 ;

LEXICON PXSG1
+PxSg1: K ;

LEXICON PXSG2
+PxSg2: K ;

LEXICON PXSP3
+PxSP3:%>s K ;

LEXICON PXPL1
+PxPl1:%>čəɬ K ;

LEXICON PXPL2
+PxPl2:%>ləp K ;
+PxPl2:%>lap K ;

! vim: set ft=xfst-lexc:


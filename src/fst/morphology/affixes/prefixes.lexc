!! Prefixes
!  --------
!! Prefixes in the Lushootseed language are bound to beginning of other words.

!! Beck, David 2018: Aspectual Affixation in Lushootseed: A minor reanalysis
!! ordering
!! Tense/mood | Poss.subj | nominalizers | Aspect | Stem | Object/voice | others
!! gʷə%<, ƛ̕u%<, tu%<, ɬu%<, bə%< | d%<, ad%< |s%<, dəxʷ%< | ʔu, ʔəs, lə, ləs, ləcu| X |%>s/%>bš, %>sut/%>ut, %>agʷəl, %>b |%>ləp, %>s, %>əxʷ

!! -1/-4?(lə=bə=ʔas-q̓ʷəl-il) proclitic Asp/Prog+:lə%<
!!-4  @U.TAM-SUBJ.SUBJ@:@U.TAM-SUBJ.SUBJ@gʷə%< (+Sbj)
!!    @U.TAM-HAB.HAB@:@U.TAM-HAB.HAB@ƛ̕u%<  (+Hab)
!!    @U.TAM-T.REM@:@U.TAM-T.REM@tu%<   (TM/Past+)
!!    @U.TAM-HAB.IRR@:@U.TAM-HAB.IRR@ɬu%<  (+Fut)
!!    @U.TAM-RE.RE@:@U.TAM-RE.RE@bə%<  (+Add)
!!-3  d%<, PxSg2+:ad%< ?| Add+:bə%< 
!!-2  NM+:s%<, dəxʷ%<
!! -1 aspect
!!   @U.TAM-A.PROG@:@U.TAM-A.PROG@lə%<  (non-circumscribed, progressive)
!! @U.TAM-A.STAT@:@U.TAM-A.STAT@ʔəs%<  (ongoing state: Asp/Stat)
!! @U.TAM-A.STAT@:@U.TAM-A.STAT@ʔas%<  (ongoing state: Asp/Stat)variant
!!   @U.TAM-A.PROGSTAT@:@U.TAM-A.PROGSTAT@ləs%< (Asp/ProgStat)
!! @U.TAM-A.PFV@:@U.TAM-A.PFV@ʔu%<  (Asp/Pfv)
!! -1 Asp/Cont+:ləcu%< (?Asp/Prog+:lə%< + Asp/Pfv+:ʔu%<) Skagit
!! -1 Asp/Stat:ʔə{sØ}{dØ}xʷ%< (Asp/Stat+:ʔəs%< + dxʷ%<)
!! 
!! X
!! 1 %>s/%>bš, %>sut/%>ut, %>agʷəl, %>b, +Inch:%>il
!! 2 %>ləp, +Px3:%>s, %>əxʷ
!! lə<bə<ləcu<gʷaadgʷad

LEXICON Prefixes
 L7_ ;

!Lonsdale 2001:211-212
!ɬubələskʷaxʷyildutəxʷčəɬ
!ɬu<bə<ləs<kʷaxʷ>y>il>d>ut>əxʷ>čəɬ
!Fut+ANEW+PgrSttv+help+YI+il+Trx+Rfx+Inc+our
!@U.TAM-HAB.IRR@@U.TAM-RE.RE@@U.TAM-A.PROGSTAT@:@U.TAM-HAB.IRR@@U.TAM-RE.RE@@U.TAM-A.PROGSTAT@ɬu%<bə%<ləs%< VERBS_OR_NOUNS ;
!@U.TAM-HAB.IRR@@U.TAM-RE.RE@@U.TAM-A.PROGSTAT@:@U.TAM-HAB.IRR@@U.TAM-RE.RE@@U.TAM-A.PROGSTAT@ɬu%<bə%<las%< VERBS_OR_NOUNS ;
!@U.TAM-HAB.IRR@@U.TAM-RE.RE@:@U.TAM-HAB.IRR@@U.TAM-RE.RE@ɬu%<bə%< DVNouns   ; ! direct to PxSg1
!gʷə%<ƛ̕u%<ʔas– Sbj+Hab+Stat
!@U.TAM-SUBJ.SUBJ@@U.TAM-HAB.HAB@@U.TAM-A.STAT@:@U.TAM-SUBJ.SUBJ@@U.TAM-HAB.HAB@@U.TAM-A.STAT@gʷə%<ƛ̕u%<ʔəs%< VERBS_OR_NOUNS ;
!@U.TAM-SUBJ.SUBJ@@U.TAM-HAB.HAB@@U.TAM-A.STAT@:@U.TAM-SUBJ.SUBJ@@U.TAM-HAB.HAB@@U.TAM-A.STAT@gʷə%<ƛ̕u%<ʔas%< VERBS_OR_NOUNS ;
!@U.TAM-SUBJ.SUBJ@@U.TAM-HAB.HAB@:@U.TAM-SUBJ.SUBJ@@U.TAM-HAB.HAB@gʷə%<ƛ̕u%< DVNouns   ; ! direct to PxSg1
!ƛ̕u%<ʔas– Hab+Stat
!@U.TAM-HAB.HAB@@U.TAM-A.STAT@:@U.TAM-HAB.HAB@@U.TAM-A.STAT@ƛ̕u%<ʔəs%< VERBS_OR_NOUNS ;
!@U.TAM-HAB.HAB@@U.TAM-A.STAT@:@U.TAM-HAB.HAB@@U.TAM-A.STAT@ƛ̕u%<ʔas%< VERBS_OR_NOUNS ;
!ʔəsq̓iʔq̓aƛ̕əb _partly cloudy_ ɬasq̓iʔq̓aƛ̕əb __partly cloudy in the future__
!ʔəsgiʔgəqəb __partly sunny__ ɬasgiʔgəqəb __partly sunny in the future__
!ʔiɬgəqil __mostly sunny__ ɬasʔiɬgəqil __mostly sunny in the future__
!ʔəsgəqəb __sunny__ ɬasgəqil __sunny in the future__
!@U.TAM-HAB.IRR@@U.TAM-A.STAT@:@U.TAM-HAB.IRR@@U.TAM-A.STAT@ɬəs%<  VERBS_OR_NOUNS ;
!@U.TAM-HAB.IRR@@U.TAM-A.STAT@:@U.TAM-HAB.IRR@@U.TAM-A.STAT@ɬas%<  VERBS_OR_NOUNS ;

!ƛ̕u=bə= Hab+Add (check ordering)
!@U.TAM-HAB.HAB@@U.TAM-RE.RE@:@U.TAM-HAB.HAB@@U.TAM-RE.RE@ƛ̕u%<bə%< DVNouns   ; ! direct to PxSg1

!bə%<ƛ̕u%< Add+Hab (check ordering)
!@U.TAM-RE.RE@@U.TAM-HAB.HAB@:@U.TAM-RE.RE@@U.TAM-HAB.HAB@bə%<ƛ̕u%< DVNouns   ; ! direct to PxSg1

!gʷə%<tu%< Sbj+Rem
!@U.TAM-SUBJ.SUBJ@@U.TAM-T.REM@:@U.TAM-SUBJ.SUBJ@@U.TAM-T.REM@gʷə%<tu%< DVNouns   ; ! direct to PxSg1

!ƛ̕u%<ʔu%< Hab+Pfv
!@U.TAM-HAB.HAB@@U.TAM-A.PFV@:@U.TAM-HAB.HAB@@U.TAM-A.PFV@ƛ̕u%<ʔu%< VERBS_OR_NOUNS ;

!ƛ̕u%<tu%<lə%< Hab+Rem+Prog
!@U.TAM-HAB.HAB@@U.TAM-T.REM@@U.TAM-A.PROG@:@U.TAM-HAB.HAB@@U.TAM-T.REM@@U.TAM-A.PROG@ƛ̕u%<tu%<lə%< VERBS_OR_NOUNS ;

!ƛ̕u%<0%<lə%< Hab+Prog
!@U.TAM-HAB.HAB@@U.TAM-A.PROG@:@U.TAM-HAB.HAB@@U.TAM-A.PROG@ƛ̕u%<lə%< VERBS_OR_NOUNS ;

!@U.TAM-A.PROG@:@U.TAM-A.PROG@lə%< Prefixes_01b ;

!bələhuy: __again/also it is done__

 
!ƛ̕u=s=ʔəs Hab+Nmz+Stat

!ɬu=d=s=ʔaʔ Irr+PxSg1+Nmlz+be.there




!!Begin
LEXICON L7_
@U.TAM-SUBJ.SUBJ@:@U.TAM-SUBJ.SUBJ@gʷə%< L6_ ; !sbj = subjunctive
@U.TAM-SUBJ.ZERO@                        L6_ ;

LEXICON L6_
@U.TAM-HAB.HAB@:@U.TAM-HAB.HAB@ƛ̕u%< L5_ ;
@U.TAM-HAB.IRR@:@U.TAM-HAB.IRR@ɬu%< L4_ ;! qəlb 'rain': ɬuqəlb 'rain in the future' is this a noun, too? _intention to_
@U.TAM-HAB.ZERO@  L5_ ;

LEXICON L5_
@U.TAM-T.REM@:@U.TAM-T.REM@tu%< L4_ ;
@U.TAM-T.ZERO@ L4_ ;

LEXICON L4_
@U.TAM-RE.RE@:@U.TAM-RE.RE@bə%< L3_ ;
@U.TAM-RE.ZERO@ L3_ ;

LEXICON L3_
@U.PossPx.Sg1@:@U.PossPx.Sg1@d%< L2_ ;
@U.PossPx.Sg2@:@U.PossPx.Sg2@ad%< L2_ ;
@U.PossPx.Pl1@:@U.PossPx.Pl1@ L2_ ;
@U.PossPx.Pl2@:@U.PossPx.Pl2@ L2_ ;
@U.PossPx.ZERO@ L2_ ;
@U.PossPx.SP3@ L2_ ;
 
LEXICON L2_
@U.NMZ.V2N@:@U.NMZ.V2N@s%< L1_ ; !Need to mark this with flags 2021-06-30
@U.NMZ.V2A@:@U.NMZ.V2A@dəxʷ%< L1_ ; 

@U.NMZ.ZERO@  L1_ ;
@U.TAM-A.CONT@:@U.TAM-A.CONT@ləcu%< VERBS_OR_NOUNS ;
@U.TAM-A.PROGSTAT@:@U.TAM-A.PROGSTAT@ləs%< VERBS_OR_NOUNS ;
@U.TAM-A.PROG@:@U.TAM-A.PROG@lə%< VERBS_OR_NOUNS ;!non-circumscribed, progressive
@U.NMZ.V2N@@U.TAM-A.PROG@:@U.NMZ.V2N@@U.TAM-A.PROG@s%<ə%< VERBS_OR_NOUNS ;
@U.NMZ.V2A@@U.TAM-A.PROG@:@U.NMZ.V2A@@U.TAM-A.PROG@dəxʷ%<ə%< VERBS_OR_NOUNS ; 


LEXICON L1_
@U.TAM-A.ZERO@:@U.TAM-A.ZERO@ VERBS_OR_NOUNS ;
@U.TAM-A.STAT@:@U.TAM-A.STAT@ʔəs%< VERBS_OR_NOUNS ; !ongoing state
@U.TAM-A.STAT@:@U.TAM-A.STAT@ʔas%< VERBS_OR_NOUNS ; !ongoing state
@U.TAM-A.PFV@:@U.TAM-A.PFV@ʔu%< VERBS_OR_NOUNS ; ! qəlb 'rain': ʔuqəlb 'raining' doesn't sound perfect to me

!!END


LEXICON VERBS_OR_NOUNS
:%{p5%}%{p4%}%{p3%}%{p2%}%{p1%}  REDUPL_FOR_VERBS ;

@U.NPrefix.ZERO@:@U.NPrefix.ZERO@%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.c@:@U.NPrefix.c@c%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
!@U.NPrefix.dexw@:@U.NPrefix.dexw@dəxʷ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.dxw@:@U.NPrefix.dxw@dxʷ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.dxws@:@U.NPrefix.dxws@dxʷs%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.je@:@U.NPrefix.je@ǰə%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.le@:@U.NPrefix.le@lə%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.s@:@U.NPrefix.s@s%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.sexw@:@U.NPrefix.sexw@səxʷ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.sxw@:@U.NPrefix.sxw@sxʷ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
!@U.NPrefix.tsi@:@U.NPrefix.tsi@tsi%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
@U.NPrefix.xh@:@U.NPrefix.xh@x̌%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
!@U.NPrefix.xhw@:@U.NPrefix.xhw@x̌ʷ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;
!@U.NPrefix.xw@:@U.NPrefix.xw@xʷ%^√%{p5%}%{p4%}%{p3%}%{p2%}%{p1%} REDUPL_FOR_NOMINALS ;

LEXICON REDUPL_FOR_VERBS
@U.REDUPL.ZERO@  Verbs ;
@U.REDUPL.123123@:@U.REDUPL.123123@%^Red123123  Verbs ;
@U.REDUPL.PL123123@:@U.REDUPL.PL123123@%^RedPL123123 Verbs ;
@U.REDUPL.12123@:@U.REDUPL.12123@%^Red12123  Verbs ;
@U.REDUPL.12q123@:@U.REDUPL.12q123@%^Red12q123  Verbs ;
@U.REDUPL.1213@:@U.REDUPL.1213@%^Red1213  Verbs ;
@U.REDUPL.12q13@:@U.REDUPL.12q13@%^Red12q13  Verbs ;
@U.REDUPL.1i123@:@U.REDUPL.1i123@%^Red1i123  Verbs ;
@U.REDUPL.1i13@:@U.REDUPL.1i13@%^Red1i13  Verbs ;
@U.REDUPL.1iq123@:@U.REDUPL.1iq123@%^Red1iq123  Verbs ;
@U.REDUPL.1iq13@:@U.REDUPL.1iq13@%^Red1iq13  Verbs ;

LEXICON REDUPL_FOR_NOMINALS
@U.REDUPL.ZERO@  NOUNS_PREFIX_PREP ;
@U.REDUPL.123123@:@U.REDUPL.123123@%^Red123123  NOUNS_PREFIX_PREP ;
@U.REDUPL.PL123123@:@U.REDUPL.PL123123@%^RedPL123123 NOUNS_PREFIX_PREP ;
@U.REDUPL.12323@:@U.REDUPL.12323@%^Red12323 NOUNS_PREFIX_PREP ;
@U.REDUPL.12q123@:@U.REDUPL.12q123@%^Red12q123 NOUNS_PREFIX_PREP ;
@U.REDUPL.12q13@:@U.REDUPL.12q13@%^Red12q13 NOUNS_PREFIX_PREP ;
@U.REDUPL.12123@:@U.REDUPL.12123@%^Red12123 NOUNS_PREFIX_PREP ;
@U.REDUPL.1213@:@U.REDUPL.1213@%^Red1213 NOUNS_PREFIX_PREP ;
@U.REDUPL.1iq123@:@U.REDUPL.1iq123@%^Red1iq123 NOUNS_PREFIX_PREP ;
@U.REDUPL.1iq13@:@U.REDUPL.1iq13@%^Red1iq13 NOUNS_PREFIX_PREP ;
@U.REDUPL.1i123@:@U.REDUPL.1i123@%^Red1i123 NOUNS_PREFIX_PREP ;
@U.REDUPL.1i13@:@U.REDUPL.1i13@%^Red1i13 NOUNS_PREFIX_PREP ;
@U.REDUPL.PL1i1i13@:@U.REDUPL.PL1i1i13@%^RedPL1i1i13 NOUNS_PREFIX_PREP ;
@U.REDUPL.2CV2CiCV@:@U.REDUPL.2CV2CiCV@%^Red2CV2CiCV NOUNS_PREFIX_PREP ;
@U.REDUPL.130130@:@U.REDUPL.130130@%^RedPL130130 NOUNS_PREFIX_PREP ;



LEXICON NOUNS_PREFIX_PREP
N_NEWWORDS ;
 Nouns ;
 Adjectives ;
 Propernouns ;

! vim: set ft=xfst-lexc:
